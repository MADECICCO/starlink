      SUBROUTINE NDF_PTSZ<T>( SCALE, ZERO, INDF, COMP, STATUS )
*+
*  Name:
*     NDF_PTSZ<T>

*  Purpose:
*     Set new scale and zero values for an NDF array component

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL NDF_PTSZ<T>( SCALE, ZERO, INDF, COMP, STATUS )

*  Description:
*     The routine sets new values for the scale and zero values
*     associated with an NDF array component. If the array is stored in
*     simple form, then the storage form is changed to scaled.
*
*     See also NDF_ZSCAL which provides a higher level interface for
*     creating scaled arrays.

*  Arguments:
*     SCALE = <TYPE> (Given)
*        The new value for the scaling factor.
*     ZERO = <TYPE> (Given)
*        The new value for the zero offset.
*     INDF = INTEGER (Given)
*        NDF identifier.
*     COMP = CHARACTER * ( * ) (Given)
*        Name of the NDF array component; 'DATA' or 'VARIANCE'.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     - There is a routine for each of the standard Fortran numerical data
*     types: integer, real and double precision. Replace the (lower case) "x"
*     in the routine name by I, R or D as appropriate.
*     -  A comma-separated list of component names may also be
*     supplied, in which case the same scale and zero values will be
*     used for each component in turn.
*     -  This routine may only be used to change the scaling of a base
*     NDF. If it is called with an array which is not a base array,
*     then it will return without action. No error will result.
*     -  An error will result if the array component, or any part of it,
*     is currently mapped for access (e.g. through another identifier).
*     -  This routine has no effect on components which are in an
*     undefined state.

*  Algorithm:
*     -  Import the NDF identifier.
*     -  Initialise the component count.
*     -  Initialise the character pointer to the start of the component
*     list. Then loop to extract each component from the list.
*     -  Find the final character of the next element in the list.
*     -  Locate the first and last non-blank characters in the element,
*     checking that it is not entirely blank.
*     -  Increment the component count.
*     -  Compare the component name with each value in turn, taking the
*     appropriate action or reporting an error message if an
*     inappropriate component name has been given.
*     -  If the component name was not recognised, then report an
*     error.
*     -  Increment the character pointer to the start of the next
*     element in the component list and return to process it.
*     -  If no error has occurred, but the number of components
*     processed is zero, then report an error.
*     -  If an error has occurred, then report context information.

*  Copyright:
*     Copyright (C) 2006 Particle Physics and Astronomy Research
*     Council. All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either version 2 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful,but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 59 Temple Place,Suite 330, Boston, MA
*     02111-1307, USA

*  Authors:
*     DSB: David S Berry (JAC)
*     {enter_new_authors_here}

*  History:
*     7-JUL-2006 (DSB):
*        Original version.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'DAT_PAR'          ! DAT_ public constants
      INCLUDE 'NDF_PAR'          ! NDF_ public constants
      INCLUDE 'NDF_CONST'        ! NDF_ private constants
      INCLUDE 'NDF_ERR'          ! NDF_ error codes

*  Global Variables:
      INCLUDE 'NDF_ACB'          ! NDF_ Access Control Block
*        ACB_DID( NDF__MXACB ) = INTEGER (Read)
*           ARY_ system identifier for the NDF's data array.
*        ACB_DMBAD( NDF__MXACB ) = LOGICAL (Write)
*           Bad pixel flag for the mapped data values.
*        ACB_DMBMD( NDF__MXACB ) = LOGICAL (Write)
*           Whether the ACB_VMBAD value has been modified.

*  Arguments Given:
      <TYPE> SCALE
      <TYPE> ZERO
      INTEGER INDF
      CHARACTER * ( * ) COMP

*  Status:
      INTEGER STATUS             ! Global status

*  External References:
      LOGICAL NDF1_SIMLR         ! String compare with abbreviation

*  Local Variables:
      INTEGER F                  ! Position of first non-blank character
      INTEGER I1                 ! Position of first component character
      INTEGER I2                 ! Position of last component character
      INTEGER IACB               ! Index to NDF entry in the ACB
      INTEGER L                  ! Position of last non-blank character
      INTEGER NCOMP              ! Number non-blank components specified
      LOGICAL THERE              ! Whether the variance array exists

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Import the NDF identifier.
      CALL NDF1_IMPID( INDF, IACB, STATUS )

*  Check that write access to the NDF is available.
      CALL NDF1_CHACC( IACB, 'WRITE', STATUS )
      IF ( STATUS .EQ. SAI__OK ) THEN

*  Initialise the component count.
         NCOMP = 0

*  Initialise the character pointer to the start of the component list.
*  Then loop to extract each element from the component list.
         I1 = 1
1        CONTINUE                ! Start of "DO WHILE" loop
         IF ( ( STATUS .EQ. SAI__OK ) .AND.
     :        ( I1 .LE. LEN( COMP ) ) ) THEN

*  Find the final character of the next element in the component list
*  (the last character before a comma or end of string).
            I2 = INDEX( COMP( I1 : ), ',' )
            IF ( I2 .EQ. 0 ) THEN
               I2 = LEN( COMP )
            ELSE
               I2 = I2 + I1 - 2
            END IF
            IF ( I2 .GE. I1 ) THEN

*  Locate the first and last non-blank characters in the element,
*  checking that it is not entirely blank.
               CALL CHR_FANDL( COMP( I1 : I2 ), F, L )
               IF ( L .GE. F ) THEN
                  F = F + I1 - 1
                  L = L + I1 - 1

*  Increment the component count.
                  NCOMP = NCOMP + 1

*  Compare the component name with each value in turn (allowing
*  abbreviation), and take the appropriate action, or report an error
*  if an inappropriate component name has been given.

*  AXIS component:
*  ==============
*  Report an error, since this component has no scaling.
                  IF ( NDF1_SIMLR( COMP( F : L ), 'AXIS',
     :                             NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_AXI',
     :               'Scale and zero values cannot be set for an ' //
     :               'AXIS component (possible programming error).',
     :               STATUS )

*  DATA component:
*  ==============
*  If the data component is mapped for access, then report an error.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'DATA',
     :                                  NDF__MINAB ) ) THEN
                     IF ( ACB_DMAP( IACB ) ) THEN

                        STATUS = NDF__ACDEN
                        CALL ERR_REP( 'NDF_PTSZ<T>_SZMP',
     :                  'Scale and zero values cannot be set for a ' //
     :                  'DATA component since the array is currently '//
     :                  'mapped for access (possible programming '//
     :                  'error).', STATUS )

*  Otherwise, use the ARY_ system to set the scale and zero values for
*  the data array.
                     ELSE
                        CALL ARY_PTSZ<T>( ACB_DID( IACB ), SCALE, ZERO,
     :                                   STATUS )
                     END IF

*  EXTENSION:
*  =========
*  Report an error, since extensions have no scaling.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'EXTENSION',
     :                                  NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_EXT',
     :               'Scale and zero values cannot be set for an ' //
     :               'EXTENSION (possible programming error).', STATUS )

*  HISTORY component:
*  =================
*  Report an error, since this component has no scaling.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'HISTORY',
     :                                  NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_HIS',
     :               'Scale and zero values cannot be set for an ' //
     :               'HISTORY component (possible programming error).',
     :               STATUS )

*  LABEL component:
*  ===============
*  Report an error, since this component has no scaling.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'LABEL',
     :                                  NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_LAB',
     :               'Scale and zero values cannot be set for an ' //
     :               'LABEL component (possible programming error).',
     :               STATUS )

*  QUALITY component:
*  =================
*  Report an error, since this component has no scaling.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'QUALITY',
     :                                  NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_QUA',
     :               'Scale and zero values cannot be set for an ' //
     :               'QUALITY component (possible programming error).',
     :               STATUS )

*  TITLE component:
*  ===============
*  Report an error, since this component has no scaling.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'TITLE',
     :                                  NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_TIT',
     :               'Scale and zero values cannot be set for an ' //
     :               'TITLE component (possible programming error).',
     :               STATUS )

*  UNITS component:
*  ===============
*  Report an error, since this component has no scaling.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'UNITS',
     :                                  NDF__MINAB ) ) THEN
                     STATUS = NDF__CNMIN
                     CALL ERR_REP( 'NDF_PTSZ<T>_UNI',
     :               'Scale and zero values cannot be set for an ' //
     :               'UNITS component (possible programming error).',
     :               STATUS )

*  VARIANCE component:
*  ==================
*  If the variance component is mapped for access, then report an error.
                  ELSE IF ( NDF1_SIMLR( COMP( F : L ), 'VARIANCE',
     :                                  NDF__MINAB ) ) THEN
                     IF ( ACB_VMAP( IACB ) ) THEN
                        STATUS = NDF__ACDEN
                        CALL ERR_REP( 'NDF_PTSZ<T>_SZMP',
     :                  'Scale and zero values cannot be set for a ' //
     :                  'VARIANCE component since the array is '//
     :                  'currently mapped for access (possible '//
     :                  'programming error).', STATUS )

*  Otherwise, ensure that variance information is available in the DCB
*  and ACB.
                     ELSE
                        CALL NDF1_VIMP( IACB, STATUS )

*  See if the ARY_ system identifier for the variance array is valid.
*  If not, then the array does not exist.
                        CALL ARY_VALID( ACB_VID( IACB ), THERE, STATUS )
                        IF ( STATUS .EQ. SAI__OK ) THEN

*  If it exists, then set the scaling.
                           IF ( THERE ) THEN
                              CALL ARY_PTSZ<T>( ACB_VID( IACB ), SCALE,
     :                                         ZERO, STATUS )
                           END IF
                        END IF
                     END IF

*  If the component name is not recognised, then report an error.
                  ELSE
                     STATUS = NDF__CNMIN
                     CALL MSG_SETC( 'BADCOMP', COMP( F : L ) )
                     CALL ERR_REP( 'NDF_PTSZ<T>_COMP',
     :                             'Invalid array component name ' //
     :                             '''^BADCOMP'' specified ' //
     :                             '(possible programming error).',
     :                             STATUS )
                  END IF
               END IF
            END IF

*  Increment the character pointer to the start of the next element in
*  the component list and return to process the next element.
            I1 = I2 + 2
            GO TO 1
         END IF

*  If no error has occurred, but no non-blank component names have been
*  processed, then report an error.
         IF ( ( STATUS .EQ. SAI__OK ) .AND. ( NCOMP .EQ. 0 ) ) THEN
            STATUS = NDF__NOCMP
            CALL ERR_REP( 'NDF_PTSZ<T>_NONE',
     :                    'No array component name specified ' //
     :                    '(possible programming error).', STATUS )
         END IF
      END IF

*  If an error occurred, then report context information and call the
*  error tracing routine.
      IF ( STATUS .NE. SAI__OK ) THEN
         CALL ERR_REP( 'NDF_PTSZ<T>_ERR',
     :   'NDF_PTSZ<T>: Error setting the scale and zero values for an '//
     :   'NDF array component.', STATUS )
         CALL NDF1_TRACE( 'NDF_PTSZ<T>', STATUS )
      END IF

      END
