      SUBROUTINE NDF1_ADI<T>( LBNDA, UBNDA, ADATA, STATUS )
*+
*  Name:
*     NDF1_ADI<T>

*  Purpose:
*     Initialise a <COMM> axis data array.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL NDF1_ADI<T>( LBNDA, UBNDA, ADATA, STATUS )

*  Description:
*     The routine assigns initial values to a <COMM> axis data array.
*     The values are chosen so as to define the default axis coordinate
*     system, in which, for each axis, the pixel with index (I) has a
*     central coordinate of (I-0.5).

*  Arguments:
*     LBNDA = INTEGER (Given)
*        Index of the first pixel on the axis.
*     UBNDA = INTEGER (Given)
*        Index of the last pixel on the axis.
*     ADATA( LBNDA : UBNDA ) = <TYPE> (Returned)
*        <COMM> axis data array.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  Successive elements of the array are set to the values
*     LBNDA-0.5, LBNDA+0.5, LBNDA+1.5, etc. (these values are rounded
*     up in the case of non-floating point types, so the values LBNDA,
*     LBNDA+1, LBNDA+2, etc. will result).
*     -  If overflow occurs because any of the values to be assigned
*     cannot be represented using the array's numeric type, then an
*     error will be reported and STATUS set.

*  Algorithm:
*     -  Note no overflow has occurred yet.
*     -  Depending on the numeric type of the array, determine the
*     lower and upper bounds on the values which may be assigned.
*     -  Loop to assign a value to each array element, checking that
*     the values are in range.
*     -  If the value is out of range, then set a "bad" value for the
*     affected array element.
*     -  If this is the first time overflow has occurred, then report
*     an error and note that overflow has occurred.

*  Copyright:
*     Copyright (C) 1990, 1992 Science & Engineering Research Council.
*     All Rights Reserved.

*  Licence:
*     This program is free software; you can redistribute it and/or
*     modify it under the terms of the GNU General Public License as
*     published by the Free Software Foundation; either version 3 of
*     the License, or (at your option) any later version.
*
*     This program is distributed in the hope that it will be
*     useful,but WITHOUT ANY WARRANTY; without even the implied
*     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
*     PURPOSE. See the GNU General Public License for more details.
*
*     You should have received a copy of the GNU General Public License
*     along with this program; if not, write to the Free Software
*     Foundation, Inc., 51 Franklin Street,Fifth Floor, Boston, MA
*     02110-1301, USA

*  Authors:
*     RFWS: R.F. Warren-Smith (STARLINK)
*     {enter_new_authors_here}

*  History:
*     13-JUN-1990 (RFWS):
*        Original version.
*     8-OCT-1990 (RFWS):
*        Added bounds checking on array values.
*     10-OCT-1990 (RFWS):
*        Fixed sign error in asignment of array values.
*     17-JUL-1992 (RFWS):
*        Changed limits LO and HI to be LBNDA and UBNDA if overflow
*        cannot occur (previous use of constants here seems to confuse
*        the mips compiler).
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'DAT_PAR'          ! DAT_ public_constants
      INCLUDE 'NDF_ERR'          ! NDF_ error codes
      INCLUDE 'PRM_PAR'          ! Primitive data constants

*  Arguments Given:
      INTEGER LBNDA
      INTEGER UBNDA

*  Arguments Given and Returned:
      <TYPE> ADATA( LBNDA : UBNDA )

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      INTEGER HI                 ! Upper bound on array values
      INTEGER I                  ! Loop counter for array elements
      INTEGER LO                 ! Lower bound on array values
      LOGICAL OVFLOW             ! Whether overflow has occurred

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion functions
      INCLUDE 'NUM_DEF_CVT'

*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Note no overflow has occurred yet.
      OVFLOW = .FALSE.

*  Depending on the numeric type of the array, determine the lower and
*  upper bounds on the values which may be assigned.
      IF ( ( '<T>' .EQ. 'D' ) .OR. ( '<T>' .EQ. 'R' ) .OR.
     :     ( '<T>' .EQ. 'I' ) ) THEN
         LO = LBNDA
         HI = UBNDA
      ELSE
         LO = NUM_<T>TOI( NUM__MIN<T> )
         HI = NUM_<T>TOI( NUM__MAX<T> )
      END IF

*  Loop to assign a value to each array element, checking that the
*  values are in range.
      DO 1 I = LBNDA, UBNDA
         IF ( ( I .GE. LO ) .AND. ( I .LE. HI ) ) THEN
            IF ( ( '<T>' .EQ. 'D' ) .OR. ( '<T>' .EQ. 'R' ) ) THEN
               ADATA( I ) = NUM_ITO<T>( I ) - NUM_DTO<T>( 0.5D0 )
            ELSE
               ADATA( I ) = NUM_ITO<T>( I )
            END IF

*  If the value is out of range, then set a "bad" value for the
*  affected array element.
         ELSE
            ADATA( I ) = VAL__BAD<T>

*  If this is the first time overflow has occurred, then report an
*  error.
            IF ( .NOT. OVFLOW ) THEN
               STATUS = NDF__AXOVF
               CALL MSG_SETR( 'AXVAL', REAL( I ) + 0.5 )
               CALL ERR_REP( 'NDF1_ADI<T>_AXVAL',
     :         'Unable to assign a value of ^AXVAL to an axis ' //
     :         'centre array with a numeric type of ''<HTYPE>''. ',
     :         STATUS )
            END IF

*  Note that overflow has occurred.
            OVFLOW = .TRUE.
         END IF
 1    CONTINUE

*  Call error tracing routine and exit.
      IF ( STATUS .NE. SAI__OK ) CALL NDF1_TRACE( 'NDF1_ADI<T>',
     :                                             STATUS )

      END
