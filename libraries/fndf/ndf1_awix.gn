      SUBROUTINE NDF1_AWI<T>( LBND, UBND, DATA, WIDTH, STATUS )
*+
*  Name:
*     NDF1_AWI<T>

*  Purpose:
*     Initialise a <COMM> axis width array.

*  Language:
*     Starlink Fortran 77

*  Invocation:
*     CALL NDF1_AWI<T>( LBND, UBND, DATA, WIDTH, STATUS )

*  Description:
*     The routine initialises a <COMM> axis width array.  The values
*     assigned are calculated from an associated axis data array giving
*     the positions of the pixel centres by forming differences between
*     the centre positions of neighbouring pixels.

*  Arguments:
*     LBND = INTEGER (Given)
*        The lower bound of the axis width array.
*     UBND = INTEGER (Given)
*        The upper bound of the axis width array.
*     DATA( LBND : UBND ) = DOUBLE PRECISION (Given)
*        Array of axis data values (containing pixel centre coordinates)
*        from which the axis width values should be derived.
*     WIDTH( LBND : UBND ) = <TYPE> (Returned)
*        The <COMM> axis width array.
*     STATUS = INTEGER (Given and Returned)
*        The global status.

*  Notes:
*     -  If overflow occurs because any of the width values cannot be
*     represented using the array's numeric type, then an error will be
*     reported and STATUS set.

*  Algorithm:
*     -  Determine the maximum axis width value that can be stored
*     without overflow.
*     -  Loop to assign values to the array elements.
*     -  If there is a pixel on either side of the current pixel, then
*     use half the separation of their centres to calculate the current
*     pixel width.
*     -  If there is a pixel on only one side, then use the distance
*     from the current pixel to calculate the width.
*     -  If there is only one pixel, then use a width of unity.
*     -  Ensure the result is positive.  If it can be stored without
*     overflow, then assign it to the width array.
*     -  Otherwise, assign a bad result.
*     -  If this is the first such error, then report it.
*     -  Note that overflow has occurred.

*  Authors:
*     RFWS: R.F. Warren-Smith (STARLINK)
*     {enter_new_authors_here}

*  History:
*     18-OCT-1990 (RFWS):
*        Original version.
*     19-OCT-1990 (RFWS):
*        Fixed bug in checking array index against bounds.
*     9-NOV-1990 (RFWS):
*        Changed the routine name and altered to perform initialisation,
*        rather than extrapolation.
*     17-JAN-1992 (RFWS):
*        Changed to make explicit checks on width values to avoid
*        overflow.
*     {enter_further_changes_here}

*  Bugs:
*     {note_any_bugs_here}

*-

*  Type Definitions:
      IMPLICIT NONE              ! No implicit typing

*  Global Constants:
      INCLUDE 'SAE_PAR'          ! Standard SAE constants
      INCLUDE 'NDF_ERR'          ! NDF_ error codes
      INCLUDE 'PRM_PAR'          ! Primitive data constants

*  Arguments Given:
      INTEGER LBND
      INTEGER UBND
      DOUBLE PRECISION DATA( LBND : UBND )

*  Arguments Returned:
      <TYPE> WIDTH( LBND : UBND )

*  Status:
      INTEGER STATUS             ! Global status

*  Local Variables:
      DOUBLE PRECISION HI        ! Maximum value which can be stored
      DOUBLE PRECISION W         ! Width value
      INTEGER I                  ! Loop counter for array elements
      LOGICAL OVFLOW             ! Whether overflow has occurred

*  Internal References:
      INCLUDE 'NUM_DEC_CVT'      ! NUM_ type conversion routines
      INCLUDE 'NUM_DEF_CVT'
*.

*  Check inherited global status.
      IF ( STATUS .NE. SAI__OK ) RETURN

*  Determine the maximum axis width value that can be stored without
*  overflow.
      HI = NUM_<T>TOD( NUM__MAX<T> )

*  Loop to assign values to the array elements.
      DO 1 I = LBND, UBND

*  If there is a pixel on either side of the current pixel, then use
*  half the separation of their centres to calculate the current pixel
*  width.
         IF ( ( I .GT. LBND ) .AND. ( I .LT. UBND ) ) THEN
            W = 0.5D0 * ( DATA( I + 1 ) - DATA( I - 1 ) )

*  If there is a pixel on only one side, then use the distance from the
*  current pixel to calculate the width.
         ELSE IF ( I .LT. UBND ) THEN
            W = DATA( I + 1 ) - DATA( I )
         ELSE IF ( I .GT. LBND ) THEN
            W = DATA( I ) - DATA( I - 1 )

*  If there is only one pixel, then use a width of unity.
         ELSE
            W = 1.0D0
         END IF

*  Ensure the result is positive.  If it can be stored without
*  overflow, then assign it to the width array.
         IF ( W .LT. 0.0D0 ) W = - W
         IF ( W .LE. HI ) THEN
            WIDTH( I ) = NUM_DTO<T>( W )

*  Otherwise, assign a bad result.
         ELSE
            WIDTH( I ) = VAL__BAD<T>

*  If this is the first such error, then report it.
            IF ( .NOT. OVFLOW ) THEN
               STATUS = NDF__AXOVF
               CALL MSG_SETI( 'PIXEL', I )
               CALL ERR_REP( 'NDF1_AWI<T>_AXVAL',
     :                       'Overflow occurred while calculating ' //
     :                       'a value for pixel ^PIXEL of an axis ' //
     :                       'width array.', STATUS )

*  Note that overflow has occurred.
               OVFLOW = .TRUE.
            END IF
         END IF
 1    CONTINUE

*  Call error tracing routine and exit.
      IF ( STATUS .NE. SAI__OK ) CALL NDF1_TRACE( 'NDF1_AWI<T>',
     :                                            STATUS )

      END
